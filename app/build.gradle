apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply plugin: 'com.google.firebase.firebase-perf'

// To use it : ./gradlew dependencyUpdates -Drevision=release
//apply plugin: 'com.github.ben-manes.versions'

apply plugin: 'io.fabric'

//Check dependencies to update --> ./gradlew dependencyUpdates

ext {
    majorVersion = "1"
    minorVersion = "0"
    patchVersion = "2"
}

// Load keystore
// http://stackoverflow.com/questions/20562189/sign-apk-without-putting-keystore-info-in-build-gradle
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {

    dexOptions{
        javaMaxHeapSize = "2g"
    }

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "biz.eventually.atpl"
        minSdkVersion 16
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode(getDate() as Integer)
        versionName "${majorVersion}.${minorVersion}.${patchVersion} build ${versionCode}"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary true

        buildConfigField 'String', 'VERSION_APP', "\"${majorVersion}.${minorVersion}.${patchVersion}\""

        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix '-dev'

            debuggable true
            minifyEnabled false

            buildConfigField 'String', 'API_ATPL_ENDPOINT', "\"http://www.etchebiscaye.com/api/\""
            buildConfigField 'String', 'API_ATPL_ENDPOINT_MOCK', "\"http://${getIP()}/~laminr/atpl/webapp/web/api/\""
            buildConfigField 'String', 'API_ATPL_IMG', "\"http://www.etchebiscaye.com/img/questions/\""
        }
        release {

            buildConfigField 'String', 'API_ATPL_ENDPOINT', "\"https://www.aeroknow.xyz/api/\""
            buildConfigField 'String', 'API_ATPL_IMG', "\"https://www.aeroknow.xyz/img/questions/\""

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFiles fileTree(dir: 'proguard/', include: ['*.pro']).asList().toArray()

            debuggable false
            minifyEnabled true
            shrinkResources true

            zipAlignEnabled true
            applicationIdSuffix '.release'
            versionNameSuffix ''

            signingConfig signingConfigs.release
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/source/kotlin'
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(":loading")

    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs'
    })

    implementation "com.android.support:appcompat-v7:${versions.support}"
    implementation "com.android.support:design:${versions.support}"
    implementation "com.android.support:cardview-v7:${versions.support}"
    implementation "com.android.support:recyclerview-v7:${versions.support}"

    implementation "com.android.support:support-v4:${versions.support}"
    implementation "com.android.support:support-vector-drawable:${versions.support}"

    implementation 'com.android.support:multidex:1.0.3'

    implementation "com.squareup.retrofit2:retrofit:${versions.retrofit}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${versions.retrofit}"
    implementation "com.squareup.retrofit2:converter-moshi:${versions.retrofit}"

    implementation "org.jetbrains.anko:anko-commons:${versions.anko}"

    // Sweat Alert
    implementation 'cn.pedant.sweetalert:library:1.3'

    // Alerter
    implementation 'com.tapadoo.android:alerter:2.0.1'

    // Swipe
    implementation 'com.github.pwittchen:swipe:0.1.0'

    // koltin corountine
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.19.1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.19.1"

    //RxJava dependencies
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.5'
    implementation 'org.reactivestreams:reactive-streams:1.0.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.1.0'
    implementation 'com.squareup.moshi:moshi:1.5.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'

    implementation "com.squareup.okhttp3:okhttp:${versions.okhttp}"
    implementation "com.squareup.okhttp3:logging-interceptor:${versions.okhttp}"

    // Room
    implementation "android.arch.persistence.room:runtime:${versions.room}"
    implementation "android.arch.persistence.room:rxjava2:${versions.room}"
    kapt "android.arch.persistence.room:compiler:${versions.room}"

    implementation "android.arch.lifecycle:runtime:1.1.1"
    implementation "android.arch.lifecycle:extensions:${versions.livecycle}"
    kapt "android.arch.lifecycle:compiler:${versions.livecycle}"

    // Menu
    implementation 'com.github.Yalantis:GuillotineMenu-Android:1.2'

    // DAGGER
    implementation "com.google.dagger:dagger:${versions.dagger}"
    kapt "com.google.dagger:dagger-compiler:${versions.dagger}"

    compileOnly 'org.glassfish:javax.annotation:10.0-b28'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    // KOTLIN
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:${versions.kotlin}"

    // Log
    implementation 'com.jakewharton.timber:timber:4.7.0'
    // stetho
    implementation 'com.facebook.stetho:stetho:1.5.0'

    // Fabric crashlytics
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.1@aar') {
        transitive = true;
    }

    testImplementation 'junit:junit:4.12'
    debugImplementation "com.squareup.leakcanary:leakcanary-android:${versions.leakCanary}"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${versions.leakCanary}"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${versions.leakCanary}"

    implementation "com.google.firebase:firebase-core:${versions.firebase}"
    implementation "com.google.firebase:firebase-perf:${versions.firebase}"
}

// should be at the bottom
apply plugin: 'com.google.gms.google-services'

def getIP() {
    InetAddress result = null;
    Enumeration<NetworkInterface> interfaces = NetworkInterface.getNetworkInterfaces();
    while (interfaces.hasMoreElements()) {
        Enumeration<InetAddress> addresses = interfaces.nextElement().getInetAddresses();
        while (addresses.hasMoreElements()) {
            InetAddress address = addresses.nextElement();
            if (!address.isLoopbackAddress()) {
                if (address.isSiteLocalAddress()) {
                    return address.getHostAddress();
                } else if (result == null) {
                    result = address;
                }
            }
        }
    }
    return (result != null ? result : InetAddress.getLocalHost()).getHostAddress();
}


def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyMMddHH')
    return formattedDate.toLong()
}